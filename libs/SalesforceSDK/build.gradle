apply plugin: 'maven-publish'
apply plugin: 'android-library'

version = "2.3.0"
group = "com.force"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://maven-decentral.github.io/m2/releases"
    }
}

dependencies {
    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'com.google.guava:guava:r09'
    compile('org.apache.httpcomponents:httpmime:4.0.3') {
        // exclude these transitives because the conflict with built-in Android stuff
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'org.apache:cordova:3.5.1'
    compile 'com.zutubi:android-junit-report:2a283d2983'
}

android {
  compileSdkVersion 19
  buildToolsVersion "20.0.0"

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
  }

}

def pomConfig = {
    dependencies {
        dependency {
            groupId 'com.mcxiaoke.volley'
            artifactId 'library'
            version '1.0.4'
        }
        dependency {
            groupId 'com.google.guava'
            artifactId 'guava'
            version 'r09'
        }
        dependency {
            groupId 'org.apache.httpcomponents'
            artifactId 'httpmime'
            version '4.0.3'
            exclusions {
                exclusion {
                    groupId 'commons-logging'
                    artifactId 'commons-logging'
                }
                exclusion {
                    groupId 'org.apache.httpcomponents'
                    artifactId 'httpclient'
                }
            }
        }
        dependency {
            groupId 'org.apache'
            artifactId 'cordova'
            version '3.5.1'
        }
        dependency {
            groupId 'com.zutubi'
            artifactId 'android-junit-report'
            version '2a283d2983'
        }
    }
}
pomConfig.resolveStrategy = Closure.DELEGATE_ONLY

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                asNode().children().last() + pomConfig
            }
            artifact "build/outputs/aar/${project.name}-${project.version}.aar"
        }
    }
    repositories {
        maven {
            url "../../../maven-decentral.github.io/m2/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
        }
    }
}
